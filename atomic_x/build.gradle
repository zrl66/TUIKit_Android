plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}


android {
    namespace 'io.trtc.tuikit.atomicx'
    compileSdk 34

    defaultConfig {
        minSdk 21
        versionName "1.0"
        versionCode 1
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    def javaVersion = JavaVersion.VERSION_1_8
    def gradleMajorVersion = gradle.gradleVersion.tokenize('.')[0].toInteger()
    if (gradleMajorVersion >= 8) {
        javaVersion = JavaVersion.VERSION_17
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }

    kotlinOptions {
        jvmTarget = javaVersion
    }


    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res-karaoke',
                    'src/main/res-callview',
            ]
        }
    }
}

dependencies {
    api 'com.google.android.material:material:1.12.0'
    api "com.google.code.gson:gson:2.8.6"
    api 'androidx.core:core-ktx:1.12.0'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    api 'com.github.bumptech.glide:glide:4.12.0'

    api rootProject.getProperties().containsKey("atomicxCoreSdk") ? rootProject.ext.atomicxCoreSdk : "io.trtc.uikit:atomicx-core:3.6.1.66"
    api rootProject.getProperties().containsKey("common") ? rootProject.ext.common : "io.trtc.uikit:common:3.3.0.1194"
    api rootProject.getChildProjects().containsKey("tuicore") ? project(':tuicore') : "com.tencent.liteav.tuikit:tuicore:8.8.7357"

    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
}
