plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

android {
    compileSdk 34
    namespace "com.tencent.qcloud.tuikit.tuicallkit"
    defaultConfig {
        minSdkVersion 21
        versionName "1.0"
        versionCode 1
    }
    lintOptions {
        disable 'StringFormatInvalid'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    def javaVersion = JavaVersion.VERSION_1_8
    def gradleMajorVersion = gradle.gradleVersion.tokenize('.')[0].toInteger()
    if (gradleMajorVersion >= 8) {
        javaVersion = JavaVersion.VERSION_17
    }

    compileOptions {
        sourceCompatibility javaVersion
        targetCompatibility javaVersion
    }
}

dependencies {
   // api fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    api project(':atomic_x')

    api 'androidx.appcompat:appcompat:1.3.1'
    api 'com.google.android.material:material:1.12.0'
    api 'androidx.recyclerview:recyclerview:1.2.1'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api "com.google.code.gson:gson:2.9.1"

    def projects = this.rootProject.getAllprojects().stream().map { project -> project.name }.collect()
    if (projects.contains("tuicore")) {
        api project(':tuicore')
    } else {
        api "com.tencent.imsdk:imsdk-plus:8.7.7201"
        api "com.tencent.liteav.tuikit:tuicore:8.7.7202"
    }
    if (projects.contains("engine_source")) {
        api project(':engine_source')
    } else {
        api rootProject.getProperties().containsKey("atomicxCoreSdk") ? rootProject.ext.atomicxCoreSdk : "io.trtc.uikit:atomicx-core:3.6.1.66"
    }
    if (projects.contains("common")) {
        api project(':common')
    } else {
        api 'io.trtc.uikit:common:3.2.+'
    }
}
